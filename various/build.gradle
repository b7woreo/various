apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    implementation lib["kotlin-stdlib"]
    implementation lib["recyclerview"]
}

install {
    repositories.mavenInstaller {
        pom {
            groupId = rootProject.group
            artifactId = 'various'
            version = rootProject.version

            project {
                name 'various'
                description rootProject.projectDescription
                url rootProject.projectUrl

                scm {
                    url rootProject.scmUrl
                    connection rootProject.scmConnection
                    developerConnection rootProject.scmDevConnection
                }

                licenses {
                    license {
                        name rootProject.licenseName
                        url rootProject.licenseUrl
                        distribution rootProject.licenseDist
                    }
                }

                developers {
                    developer {
                        id 'chrnie'
                        name 'ChenRenJie'
                    }
                }
            }
        }
    }
}

bintray {
    user = getPropertyOrNull('bintrayUser')
    key = getPropertyOrNull('bintrayKey')

    pkg {
        repo = 'various'
        name = 'various'
        licenses = [rootProject.licenseName]
        vcsUrl = rootProject.scmUrl
    }
}
